ENTRY(boot)

SECTIONS {
    /* 設定起始位址為 QEMU RISC-V virt machine 的 RAM 起始位置 */
    . = 0x80200000;

    /* 程式碼段 */
    .text : {
        KEEP(*(.text.boot));  // 保留 boot 程式碼,確保不被優化掉
        *(.text .text.*);     // 所有其他程式碼
    }

    /* 唯讀資料段 */
    .rodata : ALIGN(4) {
        *(.rodata .rodata.*);
    }

    /* 已初始化資料段 */
    .data : ALIGN(4) {
        *(.data .data.*);
    }

    /* 未初始化資料段 (BSS) */
    .bss : ALIGN(4) {
        __bss = .;                          // BSS 起始位址
        *(.bss .bss.* .sbss .sbss.*);
        __bss_end = .;                      // BSS 結束位址
    }

    /* 核心堆疊 */
    . = ALIGN(4);
    . += 128 * 1024;    /* 分配 128KB 給堆疊 */
    __stack_top = .;    /* 堆疊頂端(堆疊向下成長) */

    /* 自由記憶體區域 - 供動態分配使用 */
    . = ALIGN(4096);    /*  對齊到頁面邊界 (4KB) */
    __free_ram = .;
    
    . += 64 * 1024 * 1024;  /* 預留 64MB 空間 */
    __free_ram_end = .;
}

/* 圖解記憶體分配：---------------------------------------------------------------------------------------
0x80200000: ┌─────────────────┐
            │ .text (kernel)  │ ← 共享
            ├─────────────────┤
            │ .rodata         │ ← 共享
            ├─────────────────┤
            │ .data           │ ← 共享
            ├─────────────────┤
            │ .bss            │ ← 共享
            ├─────────────────┤
            │ kernel stack    │ ← 共享(128KB)
            ├─────────────────┤
__free_ram: │                 │
            │   Process 1     │ ← 獨立
            │   Process 2     │ ← 獨立
            │   Process 3     │ ← 獨立
            │   ...           │
            │   (動態分配)    │
            └─────────────────┘
__free_ram_end

*/

/* 各區段舉例：---------------------------------------------------------------------------------------

| 特徵                        | 位於哪個段 |
|--------------------------  |-----------|
| 有 const 關鍵字             | .rodata   |
| 字串常量 "..."              | .rodata   |
| 全域變數 “有” 初始值         | .data     |
| 全域變數 “無” 初始值         | .bss      |
| 函數內的變數                | stack     |
| malloc / new              | heap      |
| 函數程式碼                  | .text     |
*/


